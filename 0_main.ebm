macro_definition_begin
    "id": "0"
    "name": "main"
    "startup": "true"
    "periodic": [ "false" ]
    "interlock": [ "false" ]
macro_definition_end
sub execKiri(unsigned int tipKi, unsigned int tebKi, bool isTrue, bool isFalse)
	SetData(tipKi, "OMRON CJ/CS/CP", W, 0, 1)
	SetData(tebKi, "OMRON CJ/CS/CP", W, 1, 1)
end sub

sub execKanan(unsigned int tipKa, unsigned int tebKa, bool isTrue, bool isFalse)
	SetData(tipKa, "OMRON CJ/CS/CP", W, 2, 1)
	SetData(tebKa, "OMRON CJ/CS/CP", W, 3, 1)
end sub

macro_command main()

// sampling harus cocok dengan jumlah isi array datKi dan datKa
short sampling = 0, datKi[20], datKa[20]
short pointer = 0, medKi = 0, medKa = 0, prevMedKi = 0, prevMedKa = 0
float slopeKi = 0, slopeKa = 0
short pointKi = 0, pointKa = 0, valKi = 0, valKa = 0, limit = 600 
short isAutoCorrectOn = 0, corrInterval = 0, corrLimit = 0
short existCount = 0, existSampling = 30
short stdMid = 0, stdMin = 0, stdMax = 0
short corrRatioTipKi = 0, corrRatioTipKa = 0
short corrRatioTebKi = 0, corrRatioTebKa = 0
short corrMin = 0, corrMax = 0
unsigned int corrKi = 0, corrKa = 0, timeKi = 0, timeKa = 0
unsigned int tebKi = 0, tebKa = 0, tipKi = 0, tipKa = 0
bool isKiPos = 0, isKaPos = 0, isTrue = 1, isFalse = 0, isRangeSkipped = 0, isSlopeFactorUsed = 0
unsigned int timer = 0
bool isTipKi = 0, isTipKa = 0, isTebKi = 0, isTebKa = 0
short systemTime = 0, stKi = 0, stKa = 0
short corrDelay = 0, isCorrDelayDone = 0

existCount = existSampling
SetData(existCount, "Local HMI", LW, 71, 1)

while true

	// Pindahkan pointer
	GetData(pointer, "Local HMI", LW, 70, 1)
	GetData(sampling, "Local HMI", RW, 51, 1)
	if sampling > 0 and pointer < sampling then
		pointer = pointer + 1
	else
	 	pointer = 0
	end if
	SetData(pointer, "Local HMI", LW, 70, 1)
	
	// Deklarasi alamat berdasarkan pointer
	pointKi = 100 + pointer
	pointKa = 200 + pointer
	
	// Ambil data dari sensor. Jangan lupa tipe data BCD jika ambil dari PLC
	GetData(valKi, "OMRON CJ/CS/CP", D_BCD, 410, 1)
	GetData(valKa, "OMRON CJ/CS/CP", D_BCD, 420, 1)
	
	// Validasi. jika lebih dari limit, anggap nol
	if(valKi > limit) then 
		valKi = 0 
	end if
	if(valKa > limit) then 
		valKa = 0 
	end if
	
	// Simpan value berdasarkan alamat pointer
	SetData(valKi, "Local HMI", LW, pointKi, 1)
	SetData(valKa, "Local HMI", LW, pointKa, 1)
	
	// Kendali sampling
	if pointer == 0 then
		if valKi > 0 or valKa > 0 then
			// Mulai sampling (release) jika ada data
			SetData(isFalse, "Local HMI", LB, 62, 1)
		else
			// Hentikan sampling (hold) jika tidak ada data
			SetData(isTrue, "Local HMI", LB, 62, 1)
		end if
	end if	
	
	// Hitung median
	GetData(datKi[0], "Local HMI", LW, 100, sampling)
	GetData(datKa[0], "Local HMI", LW, 200, sampling)
	prevMedKi = medKi
	prevMedKa = medKa
	MEDIAN(datKi[0], medKi, sampling)
	MEDIAN(datKa[0], medKa, sampling)
	SetData(medKi, "Local HMI", LW, 10, 1)
	SetData(medKa, "Local HMI", LW, 20, 1)
	
	// Matikan mode auto  jika tidak ada data
	GetData(isAutoCorrectOn, "OMRON CJ/CS/CP", H_Bit, 10000,1)
	if medKi == 0 and medKa == 0 and isAutoCorrectOn == 1 then
		GetData(existCount, "Local HMI", LW, 71, 1)
		existCount = existCount - 1
		if existCount < 0 then
			SetData(isFalse, "OMRON CJ/CS/CP", H_Bit, 10000,1)
			existCount = existSampling
		end if		
	else
		existCount = existSampling
	end if	
	SetData(existCount, "Local HMI", LW, 71, 1)
	
	// Ambil parameter
	GetData(timer, "OMRON CJ/CS/CP", W_BCD, 6,1)
	GetData(corrLimit, "Local HMI", RW, 52, 1)
	GetData(corrInterval, "Local HMI", RW, 50, 1)
	GetData(stdMin, "Local HMI", RECIPE, "models.STD_MIN")	
	GetData(stdMax, "Local HMI", RECIPE, "models.STD_MAX")
	
	// Tentukan rentang koreksi
	corrMin = (stdMin - (corrLimit * stdMin / 100))
	corrMax = (stdMax + (corrLimit * stdMax / 100))
	SetData(corrMin, "Local HMI", LW, 53, 1)	
	SetData(corrMax, "Local HMI", LW, 54, 1)
	isTipKi = 0
	isTebKi = 0
	isTipKa = 0
	isTebKa = 0
	if valKi > corrMin and valKi < stdMin then
		isTipKi = 1
	end if
	if valKi > stdMax and valKi < corrMax then
		isTebKi = 1
	end if
	if valKa > corrMin and valKa < stdMin then
		isTipKa = 1
	end if
	if valKa > stdMax and valKa < corrMax then
		isTebKa = 1
	end if
	SetData(isTipKi, "Local HMI", LB, 14, 1)
	SetData(isTebKi, "Local HMI", LB, 15, 1)
	SetData(isTipKa, "Local HMI", LB, 24, 1)
	SetData(isTebKa, "Local HMI", LB, 25, 1)
	GetData(corrDelay, "Local HMI", RW, 53, 1)
	
	// Cek delay sebelum auto koreksi eksekusi
	if timer >= corrDelay and isAutoCorrectOn == 1 and isCorrDelayDone == 0 then
		isCorrDelayDone = 1	
		SetData(isTrue, "OMRON CJ/CS/CP", H_Bit, 015,1)
		SetData(isFalse, "OMRON CJ/CS/CP", H_Bit, 015,1)
		SetData(isFalse, "OMRON CJ/CS/CP", W_BCD, 6, 1)	
	end if
	
	if corrDelay == 0 then
		isCorrDelayDone = 1	
	end if
	
	if isAutoCorrectOn == 0 then
		isCorrDelayDone = 0	
	end if
	
	// Eksekusi auto koreksi jika auto koreksi on, interval tercapai, medki, medka, dan interval tidak kosong
	if isAutoCorrectOn == 1 and timer >= corrInterval and medKi > 0 and medKa > 0 and corrInterval > 0 and isCorrDelayDone == 1 then	
		SetData(isTrue, "OMRON CJ/CS/CP", H_Bit, 015,1)
		SetData(isFalse, "OMRON CJ/CS/CP", H_Bit, 015,1)
		SetData(isFalse, "OMRON CJ/CS/CP", W_BCD, 6, 1)	
		GetData(stdMid, "Local HMI", RECIPE, "models.STD_MID")	
		GetData(corrRatioTipKi, "Local HMI", RW, 55, 1)	
		GetData(corrRatioTipKa, "Local HMI", RW, 56, 1)
		GetData(corrRatioTebKi, "Local HMI", RW, 57, 1)	
		GetData(corrRatioTebKa, "Local HMI", RW, 58, 1)

		
		// Periksa jika standar dan rasio kosong
		if stdMid > 0 and corrRatioTipKi > 0 and corrRatioTipKa > 0 and corrRatioTebKi > 0 and corrRatioTebKa > 0 then
			
			// Periksa jika masuk standar
			corrKi = 0
			GetData(isRangeSkipped, "Local HMI", RW_Bit, 64, 1)
			if (medKi < stdMin or medKi > stdMax) or isRangeSkipped == 1 then
				// Periksa tipis atau tebal lalu hitung selisih
				if (medKi - stdMid) > 0 then
					isKiPos = 1
					corrKi = medKi - stdMid
				else if (medKi - stdMid) < 0 then 
					isKiPos = 0
					corrKi = stdMid - medKi
				end if
			end if		
			corrKa = 0
			if (medKa < stdMin or medKa > stdMax) or isRangeSkipped == 1 then
				if (medKa - stdMid) > 0 then
					isKaPos = 1
					corrKa = medKa - stdMid
				else if (medKa - stdMid) < 0 then 
					isKaPos = 0
					corrKa = stdMid - medKa
				end if
			end if				
			
			// Nolkan jika nilai koreksi melebihi rentang 
			if medKi < corrMin or medKi > corrMax then 
				corrKi = 0 
			end if
			if medKa < corrMin or medKa > corrMax then 
				corrKa = 0 
			end if
			SetData(corrKi, "Local HMI", LW, 17, 1)
			SetData(corrKa, "Local HMI", LW, 27, 1)
			
			if corrKi > 0 or corrKa > 0 then
			// get sytem time
			GetData(systemTime, "Local HMI", LW, 9030, 1)
			end if
			
			GetData(isSlopeFactorUsed, "Local HMI", RW_Bit, 65, 1)
			
			// Eksekusi ke tombol push button jika nilai koreksi ada
			if corrKi > 0 then
			
				// Hitung Slope Factor Kiri
				slopeKi = 1
				if isSlopeFactorUsed == 1 then
					slopeKi = (stdMid - medKi) / (stdMid - prevMedKi)
					if slopeKi <= 0 then
						slopeKi = 0.5
					end if
				end if
					
				if isKiPos == 1 then
					tipKi = corrKi * corrRatioTipKi * slopeKi / 1000
					tebKi = 0
					execKiri(tipKi, tebKi, isTrue, isFalse)
					SetData(isTrue, "OMRON CJ/CS/CP", H_Bit, 000, 1)
					SetData(isFalse, "OMRON CJ/CS/CP", H_Bit, 000, 1)
				else
					tipKi = 0
					tebKi = corrKi * corrRatioTebKi / 1000
					execKiri(tipKi, tebKi, isTrue, isFalse)
					SetData(isTrue, "OMRON CJ/CS/CP", H_Bit, 001, 1)
					SetData(isFalse, "OMRON CJ/CS/CP", H_Bit, 001, 1)
				end if
				// ambil waktu sistem, taro di alamat buatan kiri
				SetData(systemTime, "Local HMI", LW, 19, 1)	
			end if
			
			if corrKa > 0 then
			
				// Hitung Slope Factor Kanan
				slopeKa = 1
				if isSlopeFactorUsed == 1 then
					slopeKa = (stdMid - medKa) / (stdMid - prevMedKa)
					if slopeKa <= 0 then
						slopeKa = 0.5
					end if
				end if
				
				if isKaPos == 1 then
					tipKa = corrKa * corrRatioTipKa * slopeKa / 1000
					tebKa = 0
					execKanan(tipKa, tebKa, isTrue, isFalse)
					SetData(isTrue, "OMRON CJ/CS/CP", H_Bit, 002, 1)
					SetData(isFalse, "OMRON CJ/CS/CP", H_Bit, 002, 1)
				else
					tipKa = 0
					tebKa = corrKa * corrRatioTebKa / 1000
					execKanan(tipKa, tebKa, isTrue, isFalse)
					SetData(isTrue, "OMRON CJ/CS/CP", H_Bit, 003, 1)
					SetData(isFalse, "OMRON CJ/CS/CP", H_Bit, 003, 1)
				end if
				// ambil waktu sistem, taro di alamat buatan kanan
				SetData(systemTime, "Local HMI", LW, 29, 1)	
			end if
		
		end if
	
	end if	

wend 

end macro_command